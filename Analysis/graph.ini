//グラフオブジェクト
グラフ＝タートル！作る 消える 図形を作る．
描画＝タートル！作る．
グラフ：_XORIGIN=0.
グラフ：_YORIGIN=0.
グラフ：_WIDTHCOL=(画面！幅？)/1419．
グラフ：_HEIGHTCOL=(画面！高さ？)/766．
グラフ：_PLOT_RANGE=45.
グラフ：_XINTERVAL=100.
グラフ：_YINTERVAL=60.
グラフ：_XNUMCOL=1.
グラフ：_LABELCOL=15.
グラフ：_YLOWERLIM=0.
グラフ：_YRANGE=300.
グラフ：_DACOL=1.

グラフ：作る=「|data type| グラフ:_TYPE=type. グラフ:_DATA=data」．

グラフ：ｘ軸データ＝「｜data_arr｜
　data_arr!「 |data cnt|
　　ラベル！(data！1 読む) 作る 10 文字サイズ ((_XINTERVAL)*(cnt)) ((_YLOWERLIM)-20)  位置．
　」それぞれ実行．
　描画！(((data_arr！要素数？)+1)*(_XINTERVAL)) 歩く 図形を作る ．
」．

グラフ：y軸データ＝「｜data_arr｜
　data_arr!「 |data cnt|
　　「cnt == 1」！なら「max = (data！1 読む) 」実行．
　　「max < (data！1 読む) 」！なら「max = (data！1 読む) 」実行
　」それぞれ実行．
　digit=(max+"")!長さ？．
　max=ceil(max/(10！(digit-1) pow)).
　max=max*(10！(digit-1) pow).
　times=max/_YSCALE.
　グラフ:_DACOL=_YRANGE/max*_HEIGHTCOL.
　「|i|
　　ラベル！((i-1)*(_YSCALE)) 作る 10 文字サイズ ((_XORIGIN)-(10*digit)) (((_YRANGE)/(times))*(i-1)*(_HEIGHTCOL)+(_LABELCOL)+(_YORIGIN)) 位置.
　　描画！ペンなし (_XORIGIN) ((((_YRANGE)/(times))*(i-1)+(_YORIGIN))*(_HEIGHTCOL)) 位置 ペンあり 20 歩く 図形を作る．
　」！(times+1) 繰り返す．
　描画！((_YRANGE)*(_HEIGHTCOL)) 歩く 図形を作る 90 左回り (_XORIGIN) (_YORIGIN) 位置 ．
」．

グラフ：y軸スケール間隔＝「｜val｜
　グラフ：_YSCALE = val.
」．

グラフ:もとデータから作る=「 |f1 f2|
　「_TYPE=="棒グラフ"」！なら「
　　data_x = _DATA！（f1）射影．
　　data_y = _DATA！（f2）射影．
　　グラフ！(data_x) x軸データ．
　　グラフ！(data_y) y軸データ．
　　data_y！「| n i |
　　　n=n!1 読む．
　　　描画! ペンなし ((_XINTERVAL)*(i)) (_YORIGIN) 位置 ペンあり ((_XINTERVAL)*(i)) ((n)*(_DACOL)) 位置 (((_XINTERVAL)*(i))+(_PLOT_RANGE)) ((n)*(_DACOL)) 位置 (((_XINTERVAL)*(i))+(_PLOT_RANGE)) (_YORIGIN) 位置 図形を作る.
　　」それぞれ実行．
　」実行．
」．

グラフ:分析データから作る=「 |data_x data_y|
　「_TYPE=="棒グラフ"」！なら「
　　グラフ！(data_x) x軸データ．
　　グラフ！(data_y) y軸データ．
　　data_y！「| n i |
　　　n=n!1 読む．
　　　描画! ペンなし ((_XINTERVAL)*(i)) (_YORIGIN) 位置 ペンあり ((_XINTERVAL)*(i)) ((n)*(_DACOL)) 位置 (((_XINTERVAL)*(i))+(_PLOT_RANGE)) ((n)*(_DACOL)) 位置 (((_XINTERVAL)*(i))+(_PLOT_RANGE)) (_YORIGIN) 位置 図形を作る.
　　」それぞれ実行．
　」実行．
」．
