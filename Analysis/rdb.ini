//RDB演算の定義
テーブル：射影＝「｜;fs fn｜
　fs=配列！作る。
　fn=配列！作る。
　要素数=フィールド名!要素数?。
　_rest！「｜f｜
　　「｜n｜
　　　「(フィールド名！（n）読む）＝＝f」！なら「fs！（n）書く。fn! (f) 書く」実行。
　　」！（フィールド名！要素数？）繰り返す。
　」それぞれ実行。
　ret＝テーブル！（fn）作る。
　自分！「|n i|
　　fs!「|m j|
　　　「((i-1)%要素数)==(m-1)」!なら「ret!(n) 書く」実行。
　　」それぞれ実行。
　」それぞれ実行。
　ret。
」。

テーブル：選択＝「｜条件｜
　自分：分析結果！クリア。
　ret＝テーブル！（フィールド名）作る。
　tmp=false。
　自分！「｜r i｜
　　番号=(i-1)%(フィールド名！要素数？)。
　　f＝フィールド名！（番号+1）読む。
　　s＝""+f+"＝"+コード文字(0x22)+r+コード文字(0x22)+"。"。
　　（s）！実行。
　　//条件が正しければ戻り値にrを設定
　　「（条件！実行）」！なら「
　　　「tmp==false」!なら「tmp=番号」実行。
　　　ret!(自分！(i-tmp) 読む)　書く。
　　」実行。
　　「tmp!=false」!なら「
　　　「(i-1)%(フィールド名！要素数？)==tmp-1」!なら「tmp=false。」実行。
　　」実行。
　」それぞれ実行。
　ret。
」。

テーブル：結合=「|t|
　要素数m=自分：フィールド名！要素数？。
　要素数t=t：フィールド名！要素数？。
　「｜n｜f=自分：フィールド名！（n）読む。// 結合キーkの判別
　　「｜i｜f2＝t:フィールド名！（i）読む。
　　　「f==f2」！なら「kf=f。kn=n。ki=i」実行。
　　」！（要素数t）繰り返す。
　」！（要素数m）繰り返す。

　tmp_arr=配列！作る。
　fn＝配列！作る。// 結合後のフィールド名の取得
　自分：フィールド名！「｜f｜fn！（f）書く」それぞれ実行。
　t：フィールド名！「｜f2｜
　　　「f2！=kf」！なら「fn！（f2）書く」実行。
　」それぞれ実行。
　ret=テーブル!(fn) 作る。

　自分!「|n i|
　　「((i-1)%要素数m+1)==ki」！なら「
　　　kv=自分!(i) 読む。
　　　t!「|m j|
　　　　「((j-1)%要素数t+1)==ki」！なら「
　　　　　kv2=t!(j) 読む。
　　　　　「kv==kv2」!なら「
　　　　　　「|cnt|
　　　　　　　ret!（自分！(i+cnt-ki)　読む）書く。
　　　　　　」！(要素数m) 繰り返す。
　　　　　　「|cnt|　
　　　　　　　「cnt！＝ki」！なら「
　　　　　　　　ret!(t!(j+cnt-ki)　読む)　書く。
　　　　　　　」実行。
　　　　　　」！(要素数t) 繰り返す。
　　　　　」実行。
　　　　」実行。
　　　」それぞれ実行。
　　」実行。
　」それぞれ実行。

　「(ret!要素数?)==0」！なら「
　　mn=(自分！要素数？)/要素数m。
　　tn=(t！要素数？)/要素数t。
　　time=0。
　　time2=0。
　　「| cnt |
　　　「|i| ret!(自分!(i+time) 読む) 書く。」！(要素数m) 繰り返す。
　　　「|i| ret!(t!(i+time2) 読む) 書く。」！(要素数t) 繰り返す。
　　　time2=time2+要素数t。
　　　「time2==(t!要素数)」!なら「time2=0」実行。
　　　「(cnt%tn)==0」！なら「
　　　　time=time+要素数m。
　　　」実行。
　　」!(mn*tn)　繰り返す。
　」実行。
　ret。
」。


テーブル：小さい順＝「|並び替えたいフィールド名;quick ret res|

  並び替えたいフィールド名の番号＝０．
  「｜番号｜
    「（フィールド名！（番号）読む）＝＝並び替えたいフィールド名」！なら「
      並び替えたいフィールド名の番号＝番号。
    」実行。
  」！（フィールド名！要素数？）繰り返す。
  quick=「｜arr num num_f; ret n p left right v p番号 v番号｜
  　　ret=arr。
  　　n＝arr！要素数？。
  　　「(n/num_f)＞1」！なら「
  　　　　p番号＝n-num_f。
  　　　　left=配列！作る。
  　　　　right=配列！作る。
  　　　　「｜i;v番号｜
  　　　　　　v番号＝(i-1)*num_f。
  　　　　　　「(arr!(v番号+num)読む)<(arr!(p番号+num)読む)」！なら「
              「｜i｜
                left！（arr!(v番号+i)読む）書く
              」！（num_f）繰り返す。
            」そうでなければ「
            「｜i｜
              right！（arr!(v番号+i)読む）書く
            」！（num_f）繰り返す。
            」実行。
  　　　　」！（n/num_f-1）繰り返す。
        p＝配列！作る。
        「|i|p!(arr!(p番号+i)読む)書く」！（num_f）繰り返す。
  　　　　ret＝配列！作る（！（left）(num)(num_f)quick）（p）（！（right）(num)(num_f)quick）連結。
  　　」実行。
  　　ret。
  」。

  res=!(自分) (並び替えたいフィールド名の番号)(自分：フィールド名！要素数？)quick.

  ret=テーブル！(フィールド名)作る。
  「|n|ret!(res!(n)読む)書く」！（res!要素数？）繰り返す。
  ret.
」。

テーブル：大きい順＝「|並び替えたいフィールド名;quick ret res|

  並び替えたいフィールド名の番号＝０．
  「｜番号｜
    「（フィールド名！（番号）読む）＝＝並び替えたいフィールド名」！なら「
      並び替えたいフィールド名の番号＝番号。
    」実行。
  」！（フィールド名！要素数？）繰り返す。
  quick=「｜arr num num_f; ret n p left right v p番号 v番号｜
  　　ret=arr。
  　　n＝arr！要素数？。
  　　「(n/num_f)＞1」！なら「
  　　　　p番号＝n-num_f。
  　　　　left=配列！作る。
  　　　　right=配列！作る。
  　　　　「｜i;v番号｜
  　　　　　　v番号＝(i-1)*num_f。
  　　　　　　「(arr!(v番号+num)読む)>(arr!(p番号+num)読む)」！なら「
              「｜i｜
                left！（arr!(v番号+i)読む）書く
              」！（num_f）繰り返す。
            」そうでなければ「
            「｜i｜
              right！（arr!(v番号+i)読む）書く
            」！（num_f）繰り返す。
            」実行。
  　　　　」！（n/num_f-1）繰り返す。
        p＝配列！作る。
        「|i|p!(arr!(p番号+i)読む)書く」！（num_f）繰り返す。
  　　　　ret＝配列！作る（！（left）(num)(num_f)quick）（p）（！（right）(num)(num_f)quick）連結。
  　　」実行。
  　　ret。
  」。

  res=!(自分) (並び替えたいフィールド名の番号)(自分：フィールド名！要素数？)quick.

  ret=テーブル！(フィールド名)作る。
  「|n|ret!(res!(n)読む)書く」！（res!要素数？）繰り返す。
  ret.
」。

テーブル：重複＝「
	fieldName＝_rest!getarg　1 読む。
	two_arr＝自分！(fieldName)　射影。
　arr=配列！作る。
	「(arr!要素数?)==0」!なら「flag = true」実行.

　two_arr！「｜n｜
  arr!「｜l ｜
    「(l) == （n）」!なら　
    「flag=false」実行．
  」それぞれ実行。
  「flag == true」!なら
  「arr！（n）書く。」実行．
  flag = true.
	」それぞれ実行．
	ret = テーブル！(fieldName)作る．
	arr！「｜n｜
   		ret！(n)書く．
  」それぞれ実行．
  ret.
」．

テーブル：レコード取り出し＝「｜key num｜
　ret＝テーブル！（フィールド名）作る。
　要素数＝フィールド名!要素数？。
　自分！「｜r i｜
　　「全部!(key==ｒ) ((i-num)%要素数==0) 本当」!なら「
　　　「|j|
　　　　ret!(自分！(i-num+(j)) 読む)　書く。
　　　」！(要素数)　繰り返す。
　　」実行。
　」それぞれ実行。
　ret。
」。
